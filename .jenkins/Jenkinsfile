/*
 * Files and directories in build environment
 * * ./: PWD, contains XMHF64 source
 * * ./.git: Git directory, not used directly by Jenkins
 * * ./cache/: cache QEMU images
 * * ./qemu/: QEMU work directory
 * * ./tmp/: temporary directory for testing script
 * * ./.github/build.sh: script to build XMHF in one line
 * * ./.jenkins/Jenkinsfile: this file
 * * ./.jenkins/test3.py: test script
 * * ./.jenkins/grub.py: script to generate a minimal GRUB image
 * * ./.jenkins/boot: files to construct a minimal GRUB
 * * ./.jenkins/download.sh: download QEMU images from Google Drive
 */

parameters {
    string(name: 'XMHF_BRANCH', defaultValue: 'xmhf64', description: '')
}

void qemu_test(String subarch, String qemu_image, String qemu_image_back) {
    PWD = sh(returnStdout: true, script: 'pwd').trim()
    sh "./.jenkins/download.sh cache ${qemu_image_back}"
    sh "rm -rf tmp qemu"
    sh "mkdir tmp qemu"
    sh "ln -s ${PWD}/cache/${qemu_image_back} qemu"
    sh """
        qemu-img create -f qcow2 -b ${PWD}/qemu/${qemu_image_back} \
                        -F qcow2 ${PWD}/qemu/${qemu_image}
    """
    sh """
        python3 -u ./.jenkins/grub.py \
            --subarch ${subarch} \
            --xmhf-bin ${PWD}/ \
            --work-dir ${PWD}/tmp/ \
            --verbose \
            --boot-dir ${PWD}/.jenkins/boot
    """
    sh """
        python3 -u ./.jenkins/test3.py \
            --subarch ${subarch} \
            --qemu-image ${PWD}/qemu/${qemu_image} \
            --qemu-image-back ${PWD}/qemu/${qemu_image_back} \
            --work-dir ${PWD}/tmp/ \
            --no-display \
            --sshpass jkl \
            --verbose \
            --watch-serial
    """
}

pipeline {
    agent any

    stages {
        stage('Logistics') {
            steps {
                sh "git fetch origin ${params.XMHF_BRANCH}"
                sh "git checkout ${params.XMHF_BRANCH}"
                sh "git pull origin ${params.XMHF_BRANCH}"
                script {
                    cmt = sh(
                        returnStdout: true,
                        script: 'git rev-parse HEAD | head -c 9').trim()
                    currentBuild.displayName += " ${params.XMHF_BRANCH}"
                    currentBuild.displayName += " ${cmt}"
                }
            }
        }
        stage('Build i386') {
            steps {
                sh "git clean -Xdf"
                sh "./.github/build.sh i386"
            }
        }
        stage('Test i386') {
            steps {
                qemu_test "i386", "debian11x86-j.qcow2", "debian11x86.qcow2"
            }
        }
        stage('Build amd64') {
            steps {
                sh "git clean -Xdf"
                sh "./.github/build.sh amd64"
            }
        }
        stage('Test amd64') {
            steps {
                qemu_test "amd64", "debian11x64-j.qcow2", "debian11x64.qcow2"
            }
        }
    }
}


/*
 * @XMHF_LICENSE_HEADER_START@
 *
 * eXtensible, Modular Hypervisor Framework (XMHF)
 * Copyright (c) 2009-2012 Carnegie Mellon University
 * Copyright (c) 2010-2012 VDG Inc.
 * All Rights Reserved.
 *
 * Developed by: XMHF Team
 *               Carnegie Mellon University / CyLab
 *               VDG Inc.
 *               http://xmhf.org
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *
 * Redistributions of source code must retain the above copyright
 * notice, this list of conditions and the following disclaimer.
 *
 * Redistributions in binary form must reproduce the above copyright
 * notice, this list of conditions and the following disclaimer in
 * the documentation and/or other materials provided with the
 * distribution.
 *
 * Neither the names of Carnegie Mellon or VDG Inc, nor the names of
 * its contributors may be used to endorse or promote products derived
 * from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND
 * CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES,
 * INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS
 * BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
 * EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
 * ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
 * TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF
 * THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 *
 * @XMHF_LICENSE_HEADER_END@
 */

#include <xmhf.h>

/* gcc -m32 will define i386, but we do not want it */
#ifdef i386
#undef i386
#endif /* i386 */

#ifdef __I386__
OUTPUT_ARCH(i386)
#elif defined(__AMD64__)
OUTPUT_ARCH(i386:x86-64)
#else /* !defined(__I386__) && !defined(__AMD64__) */
#error "Unsupported Arch"
#endif /* !defined(__I386__) && !defined(__AMD64__) */

ENTRY(xmhf_runtime_entry)

MEMORY
{
  all (rwxai) : ORIGIN = __TARGET_BASE, LENGTH = 768M /* length is arbitrary */
  debug (rwxai) : ORIGIN = 0, LENGTH = 1024M
  unaccounted (rwxai) : ORIGIN = 0, LENGTH = 0 /* see section .unaccounted at end */
}

SECTIONS
{
  . = __TARGET_BASE;

  .text : {
    *(.s_rpb)
    *(.text)
    *(.text.unlikely)
    . = ALIGN(4096);
  } =0x9090

  .data : {
    *(.data)
    *(.rodata)
    *(.rodata.str1.*)
    *(.comment)
    *(.eh_frame) /* exception-metadata. might be able to discard */
    *(.bss)
    . = ALIGN(4);
    _begin_xcph_table = .;
    *(.xcph_table)
    _end_xcph_table = .;
    . = ALIGN(4096);
  } =0x9090

  /*
   * This section is marked as NOBITS in runtime.exe. Need to use
   * --set-section-flags in objcopy to include this section to runtime.bin
   */
  .palign_data : {
    *(.bss.palign_data)
    . = ALIGN(4096);
  } =0x9090

  /*
   * This section is marked as NOBITS in runtime.exe. Need to use
   * --set-section-flags in objcopy to include this section to runtime.bin
   */
  .stack : {
    *(.bss.stack)
    . = ALIGN(4096);
  } =0x9090

  /* debug sections */
  .debug_abbrev : { *(.debug_abbrev) } >debug
  .debug_aranges : { *(.debug_aranges) } >debug
  .debug_info : { *(.debug_info) } >debug
  .debug_line : { *(.debug_line) } >debug
  .debug_line_str : { *(.debug_line_str) } >debug
  .debug_loc : { *(.debug_loc) } >debug
  .debug_loclists : { *(.debug_loclists) } >debug
  .debug_ranges : { *(.debug_ranges) } >debug
  .debug_rnglists : { *(.debug_rnglists) } >debug
  .debug_str : { *(.debug_str) } >debug

  /DISCARD/ : {
    *(.note.gnu.property)
    *(.gnu.build.attributes)
  }

  /* this is to cause the link to fail if there is
   * anything we didn't explicitly place.
   * when this does cause link to fail, temporarily comment
   * this part out to see what sections end up in the output
   * which are not handled above, and handle them.
   */
  .unaccounted : {
    *(*)
  } >unaccounted

}
